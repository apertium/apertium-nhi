!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Multichar_Symbols

%<n%>                                    ! Sustantivo
%<v%>                                    ! Verbo
%<adj%>                                  ! Adjetivo
%<det%>                                  ! Determinante
%<prn%>                                  ! Pronombre
%<adv%>                                  ! Adverbio
%<np%>                                   ! Nombre propio
%<cnjsub%>                               ! Coordinación subordinada

%<tv%>                                   ! Transitivo
%<tv2%>                                  ! Bitransitivo
%<iv%>                                   ! Intransitivo

%<pl%>                                   ! Plural
%<sg%>                                   ! Singular

%<abs%>                                  ! Absolutivo

%<loc%>                                  ! Locativo

%<top%>                                  ! Topónimo

%<pro%>                                  ! Pronombre proclítico
%<pers%>                                 ! Personal, forma larga

%<dim%>                                  ! Diminutivo

%<hon%>                                  ! Honorifico


%<pres%>   ! tiempo presente
%<pret%>   ! pretérito
%<perf%>   ! perfectivo
%<impf%>   ! imperfectivo
%<fut%>    ! futuro
%<plu%>    ! pluscuamperfecto
%<opt%>    ! optativo
%<pst%>    ! pasado
%<dur%>    ! aspecto durativo

%<p1%>
%<p2%>
%<p3%>

%<px1sg%>                                ! Posesivo (1ra singular)
%<px2sg%>
%<px3sg%>
%<px1pl%>
%<px2pl%>
%<px3pl%>

%<s_sg1%>                                ! Marcador de sujeto (1ra singular)
%<s_sg2%>                                ! Marcador de sujeto (2da singular)
%<s_sg3%>
%<s_pl1%>
%<s_pl2%>
%<s_pl3%>

%<o_sg1%>                               ! Marcador de objeto directo (1ra singular)
%<o_sg2%>                               ! Marcador de objeto directo (2da singular)
%<o_sg3%>
%<o_pl1%>
%<o_pl2%>
%<o_pl3%>

%<i_sg1%>                               ! Marcador de objeto indirecto (1ra singular)
%<i_sg2%>                               ! Marcador de objeto indirecto (2da singular)
%<i_sg3%>
%<i_pl1%>
%<i_pl2%>
%<i_pl3%>

%<o_aa3%>
%<o_nn3%>

%<all%>                           ! direccional hacia el locutor

%<sent%>                                 ! Final de frase

%{o%}                                    ! o 
%{i%}                                    ! i / 0
%{h%}                                    ! h / u
%{u%}                                    ! u / h
%{H%}
%{m%}                                    ! m / n
%{q%}
%{c%}
%{s%}                                    ! para plural español
%{z%}                                    ! z/c

%>                                       ! Frontera de morfema

@P.Px.Sg@                                ! Posesivo singular
@R.Px.Sg@
@P.Px.Pl@                                ! Posesivo plural
@R.Px.Pl@
@D.Px@                                   ! Ninguna posesión
@R.Px@
@P.Sa.Sg@                                ! Sujeto singular
@R.Sa.Sg@
@P.Sa.Pl@                                ! Sujeto plural
@R.Sa.Pl@
@P.Sa.2@                                 ! Sujeto es 2da persona (necesario para el honorífico)
@R.Sa.2@
@P.Sa.3@
@D.Sa.3@
@D.Sa@                                   ! Ningún sujeto
@D.Sa.Pl@ 
@D.Sa.Sg@

@P.DOBJ.FILLED@
@D.DOBJ@

@P.PST.ON@                          ! tiempo pasado
@R.PST@
@D.PST@

@U.APPL.ON@

@P.Opt.ON@
@R.Opt@
@D.Opt@
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

NominalSubject ;
Miscellaneous ; 
Pronombres ;
Determinantes ;
Propios ;
Adverbios ;
Puntuación ;
Verb ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


!
! Verbs
!
LEXICON Verb
@P.PST.ON@:@P.PST.ON@o%>                             VerbalSubject ;
@P.PST.ON@:@P.PST.ON@y%>o%>                           VerbalSubject ; ! "ya+past"

                                                     VerbalSubject ;

LEXICON VerbalSubject
                                                      IrregularVerbs ;

@P.Sa.Sg@%<s_sg1%>:@P.Sa.Sg@ni%>                      Object ;
@P.Sa.Sg@@P.Sa.2@%<s_sg2%>:@P.Sa.Sg@@P.Sa.2@ti%>      Object ;
@P.Sa.Sg@@P.Sa.3@%<s_sg3%>:@P.Sa.Sg@@P.Sa.3@          Object ;
@P.Sa.Pl@%<s_pl1%>:@P.Sa.Pl@ti%>                      Object ;

!
! ce- subj prefix is 1pl but takes singular subject tense/aspect agreement.
!
@P.Sa.Sg@%<s_pl1%>:@P.Sa.Sg@ce%>                      Object ;

@P.Sa.Pl@%<s_pl2%>:@P.Sa.Pl@ni%>                      Object ;
@P.Sa.Pl@%<s_pl3%>:@P.Sa.Pl@                          Object ;

@P.Sa.Sg@@P.Opt.ON@%<s_sg2%>:@P.Sa.Sg@@P.Opt.ON@xi%>  Object ;
@P.Sa.Pl@@P.Opt.ON@%<s_pl2%>:@P.Sa.Pl@@P.Opt.ON@xi%>  Object ;

LEXICON Object
                                                       IV-Dir ;
@P.DOBJ.FILLED@:@P.DOBJ.FILLED@                        TV-DOBJ ;
                                                       TV2-IOBJ ;

LEXICON TV2-IOBJ
!
! Regular bitransitive construction
! (first obj is iobj, second is dobj and optional)
! e.g. ni-mitz-in-maca "te los doy", ni-mitz-maca "te (lo) doy".
!

%<i_sg1%>:nech%>                                    TV2-DOBJ ;
%<i_sg2%>:mitz%>                                    TV2-DOBJ ;
%<i_sg3%>:%{q%}%{u%}%{i%}%>                         TV2-DOBJ ;
%<i_pl1%>:tech%>                                    TV2-DOBJ ;
%<i_pl2%>:mech%>                                    TV2-DOBJ ;
%<i_pl3%>:qui%{m%}%>                                TV2-DOBJ ;

LEXICON TV2-DOBJ
@P.DOBJ.FILLED@%<o_pl3%>:@P.DOBJ.FILLED@i%{m%}%>   TV2-Dir ;
                                                   TV2-Dir ;

LEXICON TV2-Dir
%<hon%>:on%>                                       TV2-Refl ;
%<all%>:hual%>                                     TV2-Refl ;
                                                   TV2-Refl ;

LEXICON TV2-Refl
@D.DOBJ@@P.DOBJ.FILLED@%<refl%>:@D.DOBJ@@P.DOBJ.FILLED@mo%>     TV2-Indef-Obj ;
TV2-Indef-Obj ;


LEXICON TV2-Indef-Obj
@D.DOBJ@%<o_aa3%>:@D.DOBJ@te%>                     TV2-Stem ;
@D.DOBJ@%<o_nn3%>:@D.DOBJ@tla%>                    TV2-Stem ;
                                   TV2-Stem ;


LEXICON TV-DOBJ
%<o_sg1%>:nech%>             TV-Dir ;
%<o_sg2%>:mitz%>             TV-Dir ;
%<o_sg3%>:%{q%}%{u%}%{i%}%>  TV-Dir ;

! With 3sg obj (and not 3 sbj, and not honorific ), the object prrefix can be
! omitted optionally. eg "ni-0-piya" "yo lo tengo".
@D.Sa.3@%<o_sg3_om%>:@D.Sa.3@   TV-Refl ;

%<o_pl1%>:tech%>             TV-Dir ;
%<o_pl2%>:mech%>             TV-Dir ;
%<o_pl3%>:qui%{m%}%>         TV-Dir ;


LEXICON TV-Dir
%<hon%>:on%>                                       TV-Refl ;
%<all%>:hual%>                                     TV-Refl ;
                                                   TV-Refl ;

LEXICON TV-Refl
@D.DOBJ@@P.DOBJ.FILLED@%<refl%>:@D.DOBJ@@P.DOBJ.FILLED@mo%>   TV-Indef-Obj ;
                                                              TV-Indef-Obj ;


LEXICON TV-Indef-Obj
@D.DOBJ@%<o_aa3%>:@D.DOBJ@te%>                     TV-Stem ;
@D.DOBJ@%<o_nn3%>:@D.DOBJ@tla%>                    TV-Stem ;
                                                   TV-Stem ;

LEXICON IV-Dir
%<hon%>:on%>                                       IV-Stem ;
%<all%>:hual%>                                     IV-Stem ;
                                                   IV-Stem ;
LEXICON IrregularVerbs
                  Ca ;
                  Yo ;

LEXICON Ca ! estar
@D.PST@:@D.PST@ CaPres ;
@R.PST@:@R.PST@ CaImpf ;
@D.PST@:@D.PST@ CaFut ;

LEXICON Yo ! ir
@D.PST@:@D.PST@ YoPres ;
@R.PST@:@R.PST@ YoPret ;
@R.PST@:@R.PST@ YoImpf ;
@D.PST@:@D.PST@ YoFut ;


LEXICON CaPres
%<s_sg1%>ca%<v%>m%<iv%>%<pres%>:ni%>ca # ;
%<s_sg2%>ca%<v%>%<iv%>%<pres%>:ti%>ca # ;
%<s_sg3%>ca%<v%>%<iv%>%<pres%>:catqui # ;
%<s_pl1%>ca%<v%>%<iv%>%<pres%>:ti%>cate # ;
%<s_pl2%>ca%<v%>%<iv%>%<pres%>:ni%>cate # ;
%<s_pl3%>ca%<v%>%<iv%>%<pres%>:cate # ;

LEXICON CaImpf
%<s_sg1%>ca%<v%>%<iv%>%<impf%>:ni%>catca # ;
%<s_sg2%>ca%<v%>%<iv%>%<impf%>:ti%>cacta # ;
%<s_sg3%>ca%<v%>%<iv%>%<impf%>:catca # ;
%<s_pl1%>ca%<v%>%<iv%>%<impf%>:ti%>catca # ;
%<s_pl2%>ca%<v%>%<iv%>%<impf%>:ni%>catca # ;
%<s_pl3%>ca%<v%>%<iv%>%<impf%>:catca # ;

LEXICON CaFut
%<s_sg1%>ca%<v%>%<iv%>%<fut%>:ni%>izqui # ;
%<s_sg2%>ca%<v%>%<iv%>%<fut%>:ti%>izqui # ;
%<s_sg3%>ca%<v%>%<iv%>%<fut%>:izqui # ;
%<s_pl1%>ca%<v%>%<iv%>%<fut%>:ti%>izque # ;
%<s_pl2%>ca%<v%>%<iv%>%<fut%>:ni%>izque # ;
%<s_pl3%>ca%<v%>%<iv%>%<fut%>:izque # ;

LEXICON YoPres
%<s_sg1%>yo%<v%>%<iv%>%<pres%>:ni%>yo # ;
%<s_sg2%>yo%<v%>%<iv%>%<pres%>:ti%>yo # ;
%<s_sg3%>yo%<v%>%<iv%>%<pres%>:yohue # ;
%<s_pl1%>yo%<v%>%<iv%>%<pres%>:ti%>yohue # ;
%<s_pl2%>yo%<v%>%<iv%>%<pres%>:ni%>yohue # ;
%<s_pl3%>yo%<v%>%<iv%>%<pres%>:yohue # ;

LEXICON YoPret
%<s_sg1%>yo%<v%>%<iv%>%<pret%>:ni%>ya # ;
%<s_sg2%>yo%<v%>%<iv%>%<pret%>:ti%>ya # ;
%<s_sg3%>yo%<v%>%<iv%>%<pret%>:ya # ;
%<s_pl1%>yo%<v%>%<iv%>%<pret%>:ti%>yahque # ;
%<s_pl2%>yo%<v%>%<iv%>%<pret%>:ni%>yahque # ;
%<s_pl3%>yo%<v%>%<iv%>%<pret%>:yahque # ;

LEXICON YoImpf
%<s_sg1%>yo%<v%>%<iv%>%<impf%>:ni%>yaya # ;
%<s_sg2%>yo%<v%>%<iv%>%<impf%>:ti%>yaya # ;
%<s_sg3%>yo%<v%>%<iv%>%<impf%>:yaya # ;
%<s_pl1%>yo%<v%>%<iv%>%<impf%>:ti%>yaya # ;
%<s_pl2%>yo%<v%>%<iv%>%<impf%>:ni%>yaya # ;
%<s_pl3%>yo%<v%>%<iv%>%<impf%>:yaya # ;

LEXICON YoFut
%<s_sg1%>yo%<v%>%<iv%>%<fut%>:ni%>yaz # ;
%<s_sg2%>yo%<v%>%<iv%>%<fut%>:ti%>yaz # ;
%<s_sg3%>yo%<v%>%<iv%>%<fut%>:yaz # ;
%<s_pl1%>yo%<v%>%<iv%>%<fut%>:ti%>yazque # ;
%<s_pl2%>yo%<v%>%<iv%>%<fut%>:ni%>yazque # ;
%<s_pl3%>yo%<v%>%<iv%>%<fut%>:yazque # ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Continuation Verb Endings
!--------------------------
! * PresentTense
! * Imperfect
!   (In classical nahuatl, this is the same as PresentTense base (Base 1). In
!    nhi, verbs ending in 'ia' or 'oa' drop the first of those two ('i' and
!    'o', respectively), as is done in Base 1 in classical Nahuatl.
!    for eg:
!       ilhuia, "contar/decir"
!       classical:      o-ni-mitz-ilhuia-ya
!       western sierra: o-ni-mitz-ilhua-ya
!       "Yo te (lo) decía"

! * Base2Suffixes
!       * Preterite
!       * Pluperfect

! * Base3Suffixes
!       * Future
!       * Counterfactual
!       * Optative
!       * SuffixalDirectional


LEXICON IV-Stem
! dormir
cochi%<v%>%<iv%>:cochi%>  PresentTense ;
cochi%<v%>%<iv%>:cochi%>  Imperfect ;
cochi%<v%>%<iv%>:coch%>   Durative ;
cochi%<v%>%<iv%>:coch%>   Base2Suffixes ;
cochi%<v%>%<iv%>:cochi%>  Base3Suffixes ;

! salir
quiza%<v%>%<iv%>:quiza%>  PresentTense ;
quiza%<v%>%<iv%>:quiza%>  Imperfect ;
quiza%<v%>%<iv%>:quiz%>   Durative ;
quiza%<v%>%<iv%>:quiz%>   Base2Suffixes ;
quiza%<v%>%<iv%>:quiza%>  Base3Suffixes ;

! llorar
choca%<v%>%<iv%>:choca%>  PresentTense ;
choca%<v%>%<iv%>:choca%>  Imperfect ;
choca%<v%>%<iv%>:choca%>  Durative ;
choca%<v%>%<iv%>:choca%>  Base2Suffixes ;
choca%<v%>%<iv%>:choca%>  Base3Suffixes ;

! trabajar
tiquiti%<v%>%<iv%>:tiquiti%>  PresentTense;
tiquiti%<v%>%<iv%>:tiquiti%>  Imperfect;
tiquiti%<v%>%<iv%>:tiquit%>   Durative;
tiquiti%<v%>%<iv%>:tiquit%>   Base2Suffixes;
tiquiti%<v%>%<iv%>:tiquiti%>  Base3Suffixes;


LEXICON TV-Stem
! ver
itta%<v%>%<tv%>:itta%>     PresentTense ;
itta%<v%>%<tv%>:itta%>     Imperfect ;
itta%<v%>%<tv%>:itz%>      Durative ;
itta%<v%>%<tv%>:itta%>     Base2Suffixes ;
itta%<v%>%<tv%>:itta%>     Base3Suffixes ;

! poner
tlalia%<v%>%<tv%>:tlalia%> PresentTense ;
tlalia%<v%>%<tv%>:tlala%>  Imperfect ;
tlalia%<v%>%<tv%>:tlali%>  Durative ;
tlalia%<v%>%<tv%>:tlal%{i%}%{H%}%>  Base2Suffixes ;
tlalia%<v%>%<tv%>:tlali%>  Base3Suffixes ;

! decir
ihtoa%<v%>%<tv%>:ihtoa%>  PresentTense ;
ihtoa%<v%>%<tv%>:ihtoa%>  Imperfect ;
ihtoa%<v%>%<tv%>:ihtoh%>  Durative ;
ihtoa%<v%>%<tv%>:ihtoh%>  Base2Suffixes ;
ihtoa%<v%>%<tv%>:ihto%>   Base3Suffixes ;

! tener
piya%<v%>%<tv%>:piya%>  PresentTense ;
piya%<v%>%<tv%>:piya%>  Imperfect ;
piya%<v%>%<tv%>:pix%>   Durative ;
piya%<v%>%<tv%>:pix%>   Base2Suffixes ;
piya%<v%>%<tv%>:piya%>  Base3Suffixes ;

! gustar
pactia%<v%>%<tv%>:pactia%>          PresentTense ;
pactia%<v%>%<tv%>:pacta%>           Imperfect ;
pactia%<v%>%<tv%>:pactih%>          Durative ;
pactia%<v%>%<tv%>:pact%{i%}%{H%}%>  Base2Suffixes ;
pactia%<v%>%<tv%>:pacti%>           Base3Suffixes ;

! recordar
ihlnamiqui%<v%>%<tv%>:ihlnamiqui%>  PresentTense ;
ihlnamiqui%<v%>%<tv%>:ihlnamiqui%>  Imperfect ;
ihlnamiqui%<v%>%<tv%>:ihlnamic%>    Durative ;
ihlnamiqui%<v%>%<tv%>:ihlnamic%>  Base2Suffixes ;
ihlnamiqui%<v%>%<tv%>:ihlnamiqui%>  Base3Suffixes ;

! apoyar
palehuia%<v%>%<tv%>:palehuia%>         PresentTense ;
palehuia%<v%>%<tv%>:palehua%>          Imperfect ;
palehuia%<v%>%<tv%>:paleuh%>          Durative ;
palehuia%<v%>%<tv%>:palehu%{i%}%{H%}%> Base2Suffixes ;
palehuia%<v%>%<tv%>:palehui%>          Base3Suffixes ;

! sacar
quixtia%<v%>%<tv%>:quixtia%>          PresentTense;
quixtia%<v%>%<tv%>:quixta%>           Imperfect;
quixtia%<v%>%<tv%>:quix%>             Durative;
quixtia%<v%>%<tv%>:quixt%{i%}%{H%}%>  Base2Suffixes;
quixtia%<v%>%<tv%>:quixti%>           Base3Suffixes;

! querer
niqui%<v%>%<tv%>:niqui%>  PresentTense;
niqui%<v%>%<tv%>:niqui%>  Imperfect;
niqui%<v%>%<tv%>:nic%>  Durative;
niqui%<v%>%<tv%>:nic%>  Base2Suffixes;
niqui%<v%>%<tv%>:niqui%>  Base3Suffixes;

! saber
mati%<v%>%<tv%>:mati%>  PresentTense;
mati%<v%>%<tv%>:mati%>  Imperfect;
mati%<v%>%<tv%>:mat%>  Durative;
mati%<v%>%<tv%>:mat%>  Base2Suffixes;
mati%<v%>%<tv%>:mati%>  Base3Suffixes;

! amar
tlazohtla%<v%>%<tv%>:tlazohtla%>  PresentTense;
tlazohtla%<v%>%<tv%>:tlazohtla%>  Imperfect;
tlazohtla%<v%>%<tv%>:tlazohtla%>  Durative;
tlazohtla%<v%>%<tv%>:tlazohtla%>  Base2Suffixes;
tlazohtla%<v%>%<tv%>:tlazohtla%>  Base3Suffixes;

! hacer
chihua%<v%>%<tv%>:chihua%>  PresentTense;
chihua%<v%>%<tv%>:chihua%>  Imperfect;
chihua%<v%>%<tv%>:chiuh%>   Durative;
chihua%<v%>%<tv%>:chiuh%>  Base2Suffixes;
chihua%<v%>%<tv%>:chihua%>  Base3Suffixes;

! encontrar
ahci%<v%>%<tv%>:ahci%>  PresentTense;
ahci%<v%>%<tv%>:ahci%>  Imperfect;
ahci%<v%>%<tv%>:ahci%>  Durative;
ahci%<v%>%<tv%>:ahci%>  Base2Suffixes;
ahci%<v%>%<tv%>:ahci%>  Base3Suffixes;


LEXICON TV2-Stem
! dar
maca%<v%>%<tv2%>:maca%> PresentTense ;
maca%<v%>%<tv2%>:maca%> Imperfect ;
maca%<v%>%<tv2%>:maca%> Durative ;
maca%<v%>%<tv2%>:maca%> Base2Suffixes ;
maca%<v%>%<tv2%>:maca%> Base3Suffixes ;

! decir
ilhuia%<v%>%<tv2%>:ilhuia%> PresentTense ;
ilhuia%<v%>%<tv2%>:ilhua%>  Imperfect ;
ilhuia%<v%>%<tv2%>:ilhuih%> Durative ;
ilhuia%<v%>%<tv2%>:ilhu%{i%}%{H%}%> Base2Suffixes ;
ilhuia%<v%>%<tv2%>:ilhui%>  Base3Suffixes ;

LEXICON Base2Suffixes
              Preterite ;
              Pluperfect ;

LEXICON Base3Suffixes
              Future ;
              Counterfactual ;
              Optative ;
              Applicative ;
              ! TODO Suffixal Directionals (Perfective/Imperfective/Optative)


LEXICON PresentTense
@D.Sa.Pl@@D.PST@@D.Opt@%<pres%>:@D.Sa.Pl@@D.PST@@D.Opt@ # ;
@R.Sa.Pl@@D.PST@@D.Opt@%<pres%>:@R.Sa.Pl@@D.PST@@D.Opt@%{H%} # ;

LEXICON Durative
@D.Sa.Pl@@D.PST@@D.Opt@%<pres%>%<dur%>:@D.Sa.Pl@@D.PST@@D.Opt@toc # ;
@R.Sa.Pl@@D.PST@@D.Opt@%<pres%>%<dur%>:@R.Sa.Pl@@D.PST@@D.Opt@toque # ;

@D.Sa.Pl@@R.PST@@D.Opt@%<pst%>%<dur%>:@D.Sa.Pl@@R.PST@@D.Opt@to%>ya # ;
@R.Sa.Pl@@R.PST@@D.Opt@%<pst%>%<dur%>:@R.Sa.Pl@@R.PST@@D.Opt@to%>yah # ;

@D.Sa.Pl@@D.PST@@D.Opt@%<fut%>%<dur%>:@D.Sa.Pl@@D.PST@@D.Opt@to%>z # ;
@R.Sa.Pl@@D.PST@@D.Opt@%<fut%>%<dur%>:@R.Sa.Pl@@D.PST@@D.Opt@to%>zque # ;

LEXICON Imperfect
@D.Sa.Pl@@R.PST@@D.Opt@%<impf%>:@D.Sa.Pl@@R.PST@@D.Opt@ya  # ;
@R.Sa.Pl@@R.PST@@D.Opt@%<impf%>:@R.Sa.Pl@@R.PST@@D.Opt@yah # ;

LEXICON Preterite
@D.Sa.Pl@@D.Opt@@R.PST@%<pret%>:@D.Sa.Pl@@D.Opt@@R.PST@%{c%}  # ;
@R.Sa.Pl@@D.Opt@@R.PST@%<pret%>:@R.Sa.Pl@@D.Opt@@R.PST@que # ;

LEXICON Pluperfect
@D.Sa.Pl@@D.Opt@%<plu%>:@D.Sa.Pl@@D.Opt@ca   # ;
@R.Sa.Pl@@D.Opt@%<plu%>:@R.Sa.Pl@@D.Opt@cah  # ;

LEXICON Future
@D.PST@@D.Sa.Pl@@D.Opt@%<fut%>:@D.PST@@D.Sa.Pl@@D.Opt@z    # ;
@D.PST@@R.Sa.Pl@@D.Opt@%<fut%>:@D.PST@@R.Sa.Pl@@D.Opt@zque # ;

LEXICON Counterfactual
@D.PST@@D.Sa.Pl@@D.Opt@%<Cfact%>:@D.PST@@D.Sa.Pl@@D.Opt@zquia # ;
@D.PST@@R.Sa.Pl@@D.Opt@%<Cfact%>:@D.PST@@R.Sa.Pl@@D.Opt@zquiah # ;

LEXICON Optative
@D.PST@@D.Sa.Pl@@R.Opt@%<opt%>:@D.PST@@D.Sa.Pl@@R.Opt@0   # ;
@D.PST@@R.Sa.Pl@@R.Opt@%<opt%>:@D.PST@@R.Sa.Pl@@R.Opt@can # ;

LEXICON Applicative
%<appl%>:lia # ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Nominals
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON NominalSubject
! sg1: n-
! pl1: t-S-me / t-poss-S-huan

@P.Sa.Sg@%<s_sg1%>:@P.Sa.Sg@ni%>                      Predicate ;
@P.Sa.Sg@@P.Sa.2@%<s_sg2%>:@P.Sa.Sg@@P.Sa.2@ti%>      Predicate ;
@P.Sa.Sg@%<s_sg3%>:@P.Sa.Sg@                          Predicate ;
@P.Sa.Pl@%<s_pl1%>:@P.Sa.Pl@ti%>                      Predicate ;
@P.Sa.Pl@%<s_pl2%>:@P.Sa.Pl@ni%>                      Predicate ;
@P.Sa.Pl@%<s_pl3%>:@P.Sa.Pl@                          Predicate ;
                                                      Predicate ;

LEXICON Predicate
@R.Sa.2@:@R.Sa.2@                                     Honorific ;
                                                      Possession ;
                                                      Nominals ;


LEXICON Possession

@P.Px.Sg@%<px1sg%>:@P.Px.Sg@no%>               Nominals ;
@P.Px.Sg@%<px2sg%>:@P.Px.Sg@mo%>               Nominals ;
@P.Px.Sg@%<px3sg%>:@P.Px.Sg@i%>                    Nominals ;
@P.Px.Sg@%<px3sg%>:@P.Px.Sg@ni%>                    Nominals ;
@P.Px.Pl@%<px1pl%>:@P.Px.Pl@to%>               Nominals ;
@P.Px.Pl@%<px2pl%>:@P.Px.Pl@nimo%>             Nominals ;
@P.Px.Pl@%<px3pl%>:@P.Px.Pl@im%>                   Nominals ;

LEXICON Nominals 
! Sustantivos, sustantivos relacionales y otras categorias que
! pueden llevar posesión
                                                   Nouns ;
                                                   Adjetivos ;

LEXICON Honorific !TODO: add i -> honorific vowel harmony in .twolc file.
%<hon%>:on%>                   Possession ;
%<hon%>:on%>                   Nominals ;


LEXICON PlainPlural

@D.Px@@D.Sa@%<pl%>:@D.Px@@D.Sa@%>me                # ; ! Las casas
@D.Px@@R.Sa.Pl@%<pl%>:@D.Px@@R.Sa.Pl@%>me          # ; ! Ellos son las casas

LEXICON PossPlural

@R.Px@@D.Sa@%<pl%>:@R.Px@@D.Sa@%>huan              # ; ! Sus casas
@R.Px@@R.Sa.Pl@%<pl%>:@R.Px@@R.Sa.Pl@%>huan        # ; ! Ellos son sus casas

LEXICON Plural 
                                                   PossPlural ;
                                                   PlainPlural ;

LEXICON EsPlural ! TODO: implement underlying %{s%} -> (s, es) twol rule.
@D.Sa@%<pl%>:@D.Sa@%>%{e%}s                # ; ! Las casas
@R.Sa.Pl@%<pl%>:@R.Sa.Pl@%>%{e%}s          # ; ! Ellos son las casas


LEXICON Diminutivo

%<dim%>:%>tzin                                     # ; 
%<dim%>%<abs%>:%>tzin%>tli                         # ;  ! FIXME: Comprobar análisis ! Comprobar si se puede tener posesivo
%<dim%>%<pl%>:%>tzi%>tzin                          # ;  ! FIXME: Comprobar análisis

LEXICON Deriv 
                                                   Diminutivo ; 
LEXICON TlSuff
@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>tl    # ;
@D.Sa.Pl@@R.Px@%<n%>:@D.Sa.Pl@@R.Px@%>%{u%}%{h%}   # ;
%<n%>:                                             Plural ;
%<n%>:                                             Deriv ;

LEXICON ItlSuff

@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>itl   # ;
@D.Sa.Pl@@R.Px@%<n%>:@D.Sa.Pl@@R.Px@%>i%{u%}%{h%}  # ;
%<n%>:                                             Plural ;
%<n%>:                                             Deriv ;

LEXICON TliSuff

@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>tli   # ;
@D.Sa.Pl@@R.Px@%<n%>:@D.Sa.Pl@@R.Px@%>%{u%}%{h%}   # ;
%<n%>:                                             Plural ;
%<n%>:                                             Deriv ;

LEXICON LiSuff

@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>li    # ;
@D.Sa.Pl@@R.Px@%<n%>:@D.Sa.Pl@@R.Px@%>%{u%}%{h%}   # ;
%<n%>:%>li                                         Plural ;
%<n%>:                                             Deriv ;

LEXICON InSuff

@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>in    # ;
@D.Sa.Pl@@R.Px@%<n%>:@D.Sa.Pl@@R.Px@%>%{u%}%{h%}   # ;
%<n%>:                                             Plural ;
%<n%>:                                             Deriv ;

LEXICON NoSuff

@D.Sa.Pl@%<n%>:@D.Sa.Pl@                           # ;
%<n%>:                                             Plural ;
%<n%>:                                             Deriv ;

LEXICON EsSuff

@D.Sa.Pl@%<n%>:@D.Sa.Pl@                           # ;
%<n%>:                                             EsPlural ;

!LEXICON Rel 
!
!+co%<n%>%<loc%>:%>co # ; ! FIXME: Orth
!+pan%<n%>%<loc%>:%>pan # ; ! FIXME: Orth
!+tlan%<n%>%<loc%>:%>tlan # ; ! FIXME: Orth
!+yan%<n%>%<loc%>:%>yan # ; ! FIXME: Orth

LEXICON DET

%<det%>: # ; 

LEXICON Personales
!
! Formas cortas de los pronombres personales singulares.
!
nehuatl%<prn%>%<pro%>%<p1%>%<sg%>:neh # ;
tehuatl%<prn%>%<pro%>%<p2%>%<sg%>:teh # ;
yehuatl%<prn%>%<pro%>%<p3%>%<sg%>:yeh # ;

!
! Formas largas de los pronombres personales singulares.
!
nehuatl%<prn%>%<pers%>%<p1%>%<sg%>:nehuatl # ;
tehuatl%<prn%>%<pers%>%<p2%>%<sg%>:tehuatl # ;
yehuatl%<prn%>%<pers%>%<p3%>%<sg%>:yehuatl # ;

!
! Pronombres personales plurales.
!
tehuan%<prn%>%<pers%>%<p1%>%<pl%>:tehuan # ;
nimehuan%<prn%>%<pers%>%<p2%>%<pl%>:nimehuan # ;
yehuan%<prn%>%<pers%>%<p3%>%<pl%>:yehuan # ;

LEXICON Adv 

%<adv%>: # ; 

LEXICON NP-Top 

%<np%>%<top%>: # ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Miscellaneous

porque%<cnjsub%>:porque # ;
y%<cnj%>:y # ;
huan%<cnj%>:huan # ;

amo%<neg%>:amo # ;
quema%<afirm%>:quema # ;


LEXICON Determinantes 

in:in                                           DET ; ! "a"
nin:nin                                         DET ; ! "estx"
non:non                                         DET ; ! "esx"

LEXICON Pronombres 

Personales ; 

!LEXICON Relacionales 
!
!kxi:kxi Rel ; ! FIXME: Orth
!nacas:nacas Rel ; ! FIXME: Orth
!jti:jti Rel ; ! FIXME: Orth

LEXICON Nouns

altepetl:altepe                                 TlSuff ; ! "ciudad"
altipetl:altipe                                 TlSuff ; ! "ciudad"
amatl:ama                                       TlSuff ; ! "papel, libro"
atl:a                                           TlSuff ; ! "agua"
cacalotl:cacalo                                 TlSuff ; ! "cuervo"
cihuatl:cihua                                   TlSuff ; ! "mujer"
conetl:cone                                     TlSuff ; ! "bebe"
coyotl:coyo                                     TlSuff ; ! "coyote"
cozamalotl:cozamalo                             TlSuff ; ! "arcoiris"
ichcatl:ichca                                   TlSuff ; ! "borrego"
ichpocaconetl:ichpocacone                       TlSuff ; ! "bebé niña"
ihuitl:ihui                                     TlSuff ; ! "gallina"
itl:i                                           TlSuff ; ! "frijol"
nacatl:naca                                     TlSuff ; ! "carne"
oquichtl:oquich                                 TlSuff ; ! "varón"
quichconetl:quichcone                           TlSuff ; ! "bebé niño"
tapalcatihuilotl:tapalcatihuilo                 TlSuff ; ! "contenedor de agua"
ticactl:ticac                                   TlSuff ; ! "zapato"
tihmahtl:tihlmah                                TlSuff ; ! "ropa/prenda"
tipetl:tipe                                     TlSuff ; ! "cerro"
tipoztl:tipoz                                   TlSuff ; ! "carro/algo metálico"
tiquitl:tiqui                                   TlSuff ; ! "trabajo"
tlacatl:tlaca                                   TlSuff ; ! "hombre"
tlaltitl:tlalti                                 TlSuff ; ! "piedra de tierra"
tototl:toto                                     TlSuff ; ! "pájaro"
yolotl:yolo                                     TlSuff ; ! "corazon"


cintli:cin                                      TliSuff ; ! "maiz"
chantli:chan                                    TliSuff ; ! "casa"
itzcuintli:itzcuin                              TliSuff ; ! "perro"
metztli:metz                                    TliSuff ; ! "luna"
mixtli:mix                                      TliSuff ; ! "nube
pahtli:pah                                      TliSuff ; ! "medicina"
poctli:poc                                      TliSuff ; ! "humo"
tahtli:tah                                      TliSuff ; ! "papá"
tochtli:toch                                    TliSuff ; ! "conejo"

calli:cal                                       LiSuff ; ! "casa"
citlalli:citlal                                 LiSuff ; ! "estrella"
pilli:pil                                       LiSuff ; ! "hijo/a"


ayotochin:ayotoch                               InSuff ; ! "armadillo"
chacalin:chacal                                 InSuff ; ! "gusano"
chapolin:chapol                                 InSuff ; ! "chapulín"
tamazolin:tamazol                               InSuff ; ! "rana"

caxtil:caxtil                                   NoSuff ; ! "gallina"
cualcan:cualcan                                 NoSuff ; ! "mañana"
icni:icni                                       NoSuff ; ! "hermanx"
icxi:icxi                                       NoSuff ; ! "pie"
ixtololo:ixtololo                               NoSuff ; ! "ojo"
mahtlapal:mahtlapal                             NoSuff ; ! "ala"
patox:patox                                     NoSuff ; ! "pato"
tempil:tempil                                   NoSuff ; ! "pico"
tlahuical:tlahuical                             NoSuff ; ! "esposo"
tlahtol:tlahtol                                 NoSuff ; ! "palabra"
tlapitlanil:tlapitlanil                         NoSuff ; ! "rayo"
tooca:tooca                                     NoSuff ; ! "nombre"
toca:toca                                       NoSuff ; ! "nombre"
tomen:tomen                                     NoSuff ; ! "dinero"
tonal:tonal                                     NoSuff ; ! "día, sol"
yohual:yohual                                   NoSuff ; ! "noche"

caxitl:cax                                      ItlSuff ; ! "plato"
chiquihuitl:chiqui%{u%}%{h%}                    ItlSuff ; ! "canasta"
xihuitl:xi%{u%}%{h%}                            ItlSuff ; ! "hierba, año"

año:año                                         EsSuff ;
bachiller:bachiller                             EsSuff ;
bota:bota                                       EsSuff ;
cama:cama                                       EsSuff ;
campo:campo                                     EsSuff ;
clase:clase                                     EsSuff ;
compañero:compañero                             EsSuff ;
compañera:compañera                             EsSuff ;
cosa:cosa                                       EsSuff ;
escuela:escuela                                 EsSuff ;
historia:historia                               EsSuff ;
hora:hora                                       EsSuff ;
hueso:hueso                                     EsSuff ;
licencia:licencia                               EsSuff ;
lugar:lugar                                     EsSuff ;
mamá:mamá                                       EsSuff ;
masaje:masaje                                   EsSuff ;
masajista:masajista                             EsSuff ;
oportunidad:oportunidad                         EsSuff ;
palabra:palabra                                 EsSuff ;
paleta:paleta                                   EsSuff ;
papá:papá                                       EsSuff ;
primaria:primaria                               EsSuff ;
raza:raza                                       EsSuff ;
secundaria:secundaria                           EsSuff ;
semana:semana                                   EsSuff ;
tarea:tarea                                     EsSuff ;
trabajo:trabajo                                 EsSuff ;
vez:vez                                         EsSuff ;



LEXICON Adjetivos
iztac:izta               AdjCont ; !"blancx"  #TODO: i-deletion no ocurre aquí p.ej. "no+iztac" => noiztac, *noztac
xopiyahuac:xopiyahua     AdjCont ; !"verde"
tliltic:tlilti           AdjCont ; !"negrx"
chichiltic:chichilti     AdjCont ; !"rojx"
nixtic:nixti             AdjCont ; !"gris"
tomahuac:tomahua         AdjCont ; !"gordx"
pitzahuac:pitzahua       AdjCont ; !"delgadx"
chicactic:chicacti       AdjCont ; !"durx"
papatztic:papatzti       AdjCont ; !"suave"
xaxacuachtic:xaxacuachti AdjCont ; !"suave"
tzatzayactic:tzatzayacti AdjCont ; !"rotx"

tzotzocotzi:tzotzocotzi  Adj ; !"pequeñitx"
tzocotzi:tzocotzi        Adj ; !"pequeñx"

LEXICON Propios

México:México            NP-Top ; 


LEXICON AdjCont
%<adj%>:c                               Diminutivo ;
@R.Sa.Sg@%<adj%>:@R.Sa.Sg@c             #;
@D.Sa@%<adj%>:@D.Sa@c                   #;
@D.Sa.Sg@%<adj%>%<pl%>:@D.Sa.Sg@%>que   # ;


LEXICON Adj
%<adj%>:0  # ;


LEXICON Adverbios 
! TODO: Marcar con una etiqueta para indicar que son barbarismos/palabras prestadas? 
entonces:entonces            Adv ; 
pues:pues                    Adv ; 
bueno:bueno                  Adv ;
axan:axan                    Adv ; ! ahora
ayamo:ayamo                  Adv ; ! todavía no
acmo:acmo                    Adv ; ! ya no
xa:xa                        Adv ; ! quizás
cana:cana                    Adv ; ! quizás

LEXICON Puntuación

.%<sent%>:.   # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%¡%<sent%>:%¡ # ;
,%<cm%>:,     # ;
%?%<sent%>:%? # ;
…%<sent%>:…   # ;

