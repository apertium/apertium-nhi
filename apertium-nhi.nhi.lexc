!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Multichar_Symbols

%<n%>                                    ! Sustantivo
%<v%>                                    ! Verbo
%<adj%>                                  ! Adjetivo
%<det%>                                  ! Determinante
%<prn%>                                  ! Pronombre

%<tv%>                                   ! Transitivo
%<tv2%>                                  ! Bitransitivo
%<iv%>                                   ! Intransitivo

%<pl%>                                   ! Plural
%<sg%>                                   ! Singular

%<abs%>                                  ! Absolutivo

%<loc%>                                  ! Locativo

%<pro%>                                  ! Pronombre proclítico
%<pers%>                                 ! Personal, forma larga

%<dim%>                                  ! Diminutivo

%<hon%>                                  ! Honorifico


%<pres%>   ! tiempo presente
%<pret%>   ! pretérito
%<impf%>   ! imperfectivo
%<fut%>    ! futuro
%<plu%>    ! pluscuamperfecto
%<opt%>    ! optativo


%<p1%>
%<p2%>
%<p3%>

%<px1sg%>                                ! Posesivo (1ra singular)
%<px2sg%>
%<px3sg%>
%<px1pl%>
%<px2pl%>
%<px3pl%>

%<s_sg1%>                                ! Marcador de sujeto (1ra singular)
%<s_sg2%>                                ! Marcador de sujeto (2da singular)
%<s_sg3%>
%<s_pl1%>
%<s_pl2%>
%<s_pl3%>

%<o_sg1%>                               ! Marcador de objeto directo (1ra singular)
%<o_sg2%>                               ! Marcador de objeto directo (2da singular)
%<o_sg3%>
%<o_pl1%>
%<o_pl2%>
%<o_pl3%>

%<i_sg1%>                               ! Marcador de objeto indirecto (1ra singular)
%<i_sg2%>                               ! Marcador de objeto indirecto (2da singular)
%<i_sg3%>
%<i_pl1%>
%<i_pl2%>
%<i_pl3%>

%<o_aa3%>
%<o_nn3%>

%<all%>                           ! direccional hacia el locutor

%<sent%>                                 ! Final de frase

%{o%}                                    ! o 
%{i%}                                    ! i / 0
%{h%}                                    ! h / u
%{u%}                                    ! u / h
%{H%}
%{m%}                                    ! m / n
%{q%}
%{c%}
%>                                       ! Frontera de morfema

@P.Px.Sg@                                ! Posesivo singular
@R.Px.Sg@
@P.Px.Pl@                                ! Posesivo plural
@R.Px.Pl@
@D.Px@                                   ! Ninguna posesión
@R.Px@
@P.Sa.Sg@                                ! Sujeto singular
@R.Sa.Sg@
@P.Sa.Pl@                                ! Sujeto plural
@R.Sa.Pl@
@P.Sa.2@                                 ! Sujeto es 2da persona (necesario para el honorífico)
@R.Sa.2@
@P.Sa.3@
@D.Sa.3@
@D.Sa@                                   ! Ningún sujeto
@D.Sa.Pl@ 
@D.Sa.Sg@

@P.DOBJ.FILLED@
@D.DOBJ@

@P.PST.ON@                          ! tiempo pasado
@R.PST@
@D.PST@

@U.APPL.ON@

@P.Opt.ON@
@R.Opt@
@D.Opt@
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

NominalSubject ;
Pronombres ;
Determinantes ;
Puntuación ;
Verb ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


!
! Verbs
!
LEXICON Verb
@P.PST.ON@:@P.PST.ON@o%>                             VerbalSubject ;
                                                     VerbalSubject ;

LEXICON VerbalSubject
                                                      IrregularVerbs ;

@P.Sa.Sg@%<s_sg1%>:@P.Sa.Sg@ni%>                      Object ;
@P.Sa.Sg@@P.Sa.2@%<s_sg2%>:@P.Sa.Sg@@P.Sa.2@ti%>      Object ;
@P.Sa.Sg@@P.Sa.3@%<s_sg3%>:@P.Sa.Sg@@P.Sa.3@          Object ;
@P.Sa.Pl@%<s_pl1%>:@P.Sa.Pl@ti%>                      Object ;
@P.Sa.Pl@%<s_pl2%>:@P.Sa.Pl@ni%>                      Object ;
@P.Sa.Pl@%<s_pl3%>:@P.Sa.Pl@                          Object ;

@P.Sa.Sg@@P.Opt.ON@%<s_sg2%>:@P.Sa.Sg@@P.Opt.ON@xi%>  Object ;
@P.Sa.Pl@@P.Opt.ON@%<s_pl2%>:@P.Sa.Pl@@P.Opt.ON@xi%>  Object ;

LEXICON Object
                                                       IV-Dir ;
@P.DOBJ.FILLED@:@P.DOBJ.FILLED@                        TV-DOBJ ;
                                                       TV2-IOBJ ;

LEXICON TV2-IOBJ
!
! Regular bitransitive construction
! (first obj is iobj, second is dobj and optional)
! e.g. ni-mitz-in-maca "te los doy", ni-mitz-maca "te (lo) doy".
!

%<i_sg1%>:nech%>                                    TV2-DOBJ ;
%<i_sg2%>:mitz%>                                    TV2-DOBJ ;
%<i_sg3%>:%{q%}%{u%}%{i%}%>                         TV2-DOBJ ;
%<i_pl1%>:tech%>                                    TV2-DOBJ ;
%<i_pl2%>:mech%>                                    TV2-DOBJ ;
%<i_pl3%>:qui%{m%}%>                                TV2-DOBJ ;

LEXICON TV2-DOBJ
@P.DOBJ.FILLED@%<o_pl3%>:@P.DOBJ.FILLED@i%{m%}%>   TV2-Dir ;
                                                   TV2-Dir ;

LEXICON TV2-Dir
%<hon%>:on%>                                       TV2-Refl ;
%<all%>:hual%>                                     TV2-Refl ;
                                                   TV2-Refl ;

LEXICON TV2-Refl
@D.DOBJ@@P.DOBJ.FILLED@%<refl%>:@D.DOBJ@@P.DOBJ.FILLED@mo%>     TV2-Indef-Obj ;
TV2-Indef-Obj ;


LEXICON TV2-Indef-Obj
@D.DOBJ@%<o_aa3%>:@D.DOBJ@te%>                     TV2-Stem ;
@D.DOBJ@%<o_nn3%>:@D.DOBJ@tla%>                    TV2-Stem ;
                                   TV2-Stem ;


LEXICON TV-DOBJ
%<o_sg1%>:nech%>             TV-Dir ;
%<o_sg2%>:mitz%>             TV-Dir ;
%<o_sg3%>:%{q%}%{u%}%{i%}%>  TV-Dir ;

! With 3sg obj (and not 3 sbj, and not honorific ), the object prrefix can be
! omitted optionally. eg "ni-0-piya" "yo lo tengo".
@D.Sa.3@%<o_sg3_om%>:@D.Sa.3@   TV-Refl ;

%<o_pl1%>:tech%>             TV-Dir ;
%<o_pl2%>:mech%>             TV-Dir ;
%<o_pl3%>:qui%{m%}%>         TV-Dir ;


LEXICON TV-Dir
%<hon%>:on%>                                       TV-Refl ;
%<all%>:hual%>                                     TV-Refl ;
                                                   TV-Refl ;

LEXICON TV-Refl
@D.DOBJ@@P.DOBJ.FILLED@%<refl%>:@D.DOBJ@@P.DOBJ.FILLED@mo%>   TV-Indef-Obj ;
                                                              TV-Indef-Obj ;


LEXICON TV-Indef-Obj
@D.DOBJ@%<o_aa3%>:@D.DOBJ@te%>                     TV-Stem ;
@D.DOBJ@%<o_nn3%>:@D.DOBJ@tla%>                    TV-Stem ;
                                                   TV-Stem ;

LEXICON IV-Dir
%<hon%>:on%>                                       IV-Stem ;
%<all%>:hual%>                                     IV-Stem ;
                                                   IV-Stem ;
LEXICON IrregularVerbs
                  Ca ;

LEXICON Ca ! estar
@D.PST@:@D.PST@ CaPres ;
@R.PST@:@R.PST@ CaImpf ;
@D.PST@:@D.PST@ CaFut ;


LEXICON CaPres
%<s_sg1%>ca%<v%>m%<iv%>%<pres%>:nica # ;
%<s_sg2%>ca%<v%>%<iv%>%<pres%>:tica # ;
%<s_sg3%>ca%<v%>%<iv%>%<pres%>:catci # ;
%<s_pl1%>ca%<v%>%<iv%>%<pres%>:ticate # ;
%<s_pl2%>ca%<v%>%<iv%>%<pres%>:nicate # ;
%<s_pl3%>ca%<v%>%<iv%>%<pres%>:cate # ;

LEXICON CaImpf
%<s_sg1%>ca%<v%>%<iv%>%<impf%>:nicatca # ;
%<s_sg2%>ca%<v%>%<iv%>%<impf%>:ticacta # ;
%<s_sg3%>ca%<v%>%<iv%>%<impf%>:catca # ;
%<s_pl1%>ca%<v%>%<iv%>%<impf%>:ticatca # ;
%<s_pl2%>ca%<v%>%<iv%>%<impf%>:nicatca # ;
%<s_pl3%>ca%<v%>%<iv%>%<impf%>:catca # ;


LEXICON CaFut
%<s_sg1%>ca%<v%>%<iv%>%<fut%>:ni%>izqui # ;
%<s_sg2%>ca%<v%>%<iv%>%<fut%>:ti%>izqui # ;
%<s_sg3%>ca%<v%>%<iv%>%<fut%>:izqui # ;
%<s_pl1%>ca%<v%>%<iv%>%<fut%>:ti%>izque # ;
%<s_pl2%>ca%<v%>%<iv%>%<fut%>:ni%>izque # ;
%<s_pl3%>ca%<v%>%<iv%>%<fut%>:izque # ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Continuation Verb Endings
!--------------------------
! * PresentTense
! * Imperfect
!   (In classical nahuatl, this is the same as PresentTense base (Base 1). In
!    nhi, verbs ending in 'ia' or 'oa' drop the first of those two ('i' and
!    'o', respectively), as is done in Base 1 in classical Nahuatl.
!    for eg:
!       ilhuia, "contar/decir"
!       classical:      o-ni-mitz-ilhuia-ya
!       western sierra: o-ni-mitz-ilhua-ya
!       "Yo te (lo) decía"

! * Base2Suffixes
!       * Preterite
!       * Pluperfect

! * Base3Suffixes
!       * Future
!       * Counterfactual
!       * Optative
!       * SuffixalDirectional


LEXICON IV-Stem
cochi%<v%>%<iv%>:cochi%>  PresentTense ;
cochi%<v%>%<iv%>:cochi%>  Imperfect ;
cochi%<v%>%<iv%>:coch%>   Base2Suffixes ;
cochi%<v%>%<iv%>:cochi%>  Base3Suffixes ;

quiza%<v%>%<iv%>:quiza%>  PresentTense ;
quiza%<v%>%<iv%>:quiza%>  Imperfect ;
quiza%<v%>%<iv%>:quiz%>   Base2Suffixes ;
quiza%<v%>%<iv%>:quiza%>  Base3Suffixes ;

choca%<v%>%<iv%>:choca%>  PresentTense ;
choca%<v%>%<iv%>:choca%>  Imperfect ;
choca%<v%>%<iv%>:choca%>  Base2Suffixes ;
choca%<v%>%<iv%>:choca%>  Base3Suffixes ;

LEXICON TV-Stem
itta%<v%>%<tv%>:itta%>     PresentTense ;
itta%<v%>%<tv%>:itta%>     Imperfect ;
itta%<v%>%<tv%>:itta%>     Base2Suffixes ;
itta%<v%>%<tv%>:itta%>     Base3Suffixes ;

tlalia%<v%>%<tv%>:tlalia%> PresentTense ;
tlalia%<v%>%<tv%>:tlala%>  Imperfect ;
tlalia%<v%>%<tv%>:tlali%>  Base2Suffixes ;
tlalia%<v%>%<tv%>:tlali%>  Base3Suffixes ;

ihtoa%<v%>%<tv%>:ihtoa%> PresentTense ;
ihtoa%<v%>%<tv%>:ihtoa%>  Imperfect ;
ihtoa%<v%>%<tv%>:ihtoh%>  Base2Suffixes ;
ihtoa%<v%>%<tv%>:ihto%>  Base3Suffixes ;

piya%<v%>%<tv%>:piya%> PresentTense ;
piya%<v%>%<tv%>:piya%>  Imperfect ;
piya%<v%>%<tv%>:pix%>  Base2Suffixes ;
piya%<v%>%<tv%>:piya%>  Base3Suffixes ;

pactia%<v%>%<tv%>:pactia%> PresentTense ;
pactia%<v%>%<tv%>:pacta%>  Imperfect ;
pactia%<v%>%<tv%>:pacti%{H%}%>  Base2Suffixes ;
pactia%<v%>%<tv%>:pactiam%>  Base3Suffixes ;

LEXICON TV2-Stem
maca%<v%>%<tv2%>:maca%> PresentTense ;
maca%<v%>%<tv2%>:maca%> Imperfect ;
maca%<v%>%<tv2%>:maca%> Base2Suffixes ;
maca%<v%>%<tv2%>:maca%> Base3Suffixes ;

ilhuia%<v%>%<tv2%>:ilhuia%> PresentTense ;
ilhuia%<v%>%<tv2%>:ilhua%>  Imperfect ;
ilhuia%<v%>%<tv2%>:ilhu%{i%}%{H%}%> Base2Suffixes ;
ilhuia%<v%>%<tv2%>:ilhui%>  Base3Suffixes ;

LEXICON Base2Suffixes
              Preterite ;
              Pluperfect ;

LEXICON Base3Suffixes
              Future ;
              Counterfactual ;
              Optative ;
              Applicative ;
              ! TODO Suffixal Directionals (Perfective/Imperfective/Optative)


LEXICON PresentTense
@D.Sa.Pl@@D.PST@@D.Opt@%<pres%>:@D.Sa.Pl@@D.PST@@D.Opt@ # ;
@R.Sa.Pl@@D.PST@@D.Opt@%<pres%>:@R.Sa.Pl@@D.PST@@D.Opt@h # ;

LEXICON Imperfect
@D.Sa.Pl@@R.PST@@D.Opt@%<impf%>:@D.Sa.Pl@@R.PST@@D.Opt@ya  # ;
@R.Sa.Pl@@R.PST@@D.Opt@%<impf%>:@R.Sa.Pl@@R.PST@@D.Opt@yah # ;

LEXICON Preterite
@D.Sa.Pl@@D.Opt@@R.PST@%<pret%>:@D.Sa.Pl@@D.Opt@@R.PST@%{c%}  # ;
@R.Sa.Pl@@D.Opt@@R.PST@%<pret%>:@R.Sa.Pl@@D.Opt@@R.PST@que # ;

LEXICON Pluperfect
@D.Sa.Pl@@D.Opt@%<plu%>:@D.Sa.Pl@@D.Opt@ca   # ;
@R.Sa.Pl@@D.Opt@%<plu%>:@R.Sa.Pl@@D.Opt@cah  # ;

LEXICON Future
@D.PST@@D.Sa.Pl@@D.Opt@%<fut%>:@D.PST@@D.Sa.Pl@@D.Opt@z    # ;
@D.PST@@R.Sa.Pl@@D.Opt@%<fut%>:@D.PST@@R.Sa.Pl@@D.Opt@zque # ;

LEXICON Counterfactual
@D.PST@@D.Sa.Pl@@D.Opt@%<Cfact%>:@D.PST@@D.Sa.Pl@@D.Opt@zquia # ;
@D.PST@@R.Sa.Pl@@D.Opt@%<Cfact%>:@D.PST@@R.Sa.Pl@@D.Opt@zquiah # ;

LEXICON Optative
@D.PST@@D.Sa.Pl@@R.Opt@%<opt%>:@D.PST@@D.Sa.Pl@@R.Opt@0   # ;
@D.PST@@R.Sa.Pl@@R.Opt@%<opt%>:@D.PST@@R.Sa.Pl@@R.Opt@can # ;

LEXICON Applicative
%<appl%>:lia # ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Nominals
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON NominalSubject
! sg1: n-
! pl1: t-S-me / t-poss-S-huan

@P.Sa.Sg@%<s_sg1%>:@P.Sa.Sg@ni%>                      Predicate ;
@P.Sa.Sg@@P.Sa.2@%<s_sg2%>:@P.Sa.Sg@@P.Sa.2@ti%>      Predicate ;
@P.Sa.Sg@%<s_sg3%>:@P.Sa.Sg@                          Predicate ;
@P.Sa.Pl@%<s_pl1%>:@P.Sa.Pl@ti%>                      Predicate ;
@P.Sa.Pl@%<s_pl2%>:@P.Sa.Pl@ni%>                      Predicate ;
@P.Sa.Pl@%<s_pl3%>:@P.Sa.Pl@                          Predicate ;
                                                      Predicate ;

LEXICON Predicate
@R.Sa.2@:@R.Sa.2@                                     Honorific ;
                                                      Possession ;
                                                      Nominals ;


LEXICON Possession

@P.Px.Sg@%<px1sg%>:@P.Px.Sg@n%{o%}%>               Nominals ;
@P.Px.Sg@%<px2sg%>:@P.Px.Sg@m%{o%}%>               Nominals ;
@P.Px.Sg@%<px3sg%>:@P.Px.Sg@i%>                    Nominals ;
@P.Px.Pl@%<px1pl%>:@P.Px.Pl@t%{o%}%>               Nominals ;
@P.Px.Pl@%<px2pl%>:@P.Px.Pl@nim%{o%}%>             Nominals ;
@P.Px.Pl@%<px3pl%>:@P.Px.Pl@im%>                   Nominals ;

LEXICON Nominals 
! Sustantivos, sustantivos relacionales y otras categorias que
! pueden llevar posesión
                                                   Nouns ;
                                                   Adj ;

LEXICON Honorific !TODO: add i -> honorific vowel harmony in .twolc file.
%<hon%>:on%>                   Possession ;
%<hon%>:on%>                   Nominals ;


LEXICON PlainPlural

@D.Px@@D.Sa@%<pl%>:@D.Px@@D.Sa@%>me                # ; ! Las casas
@D.Px@@R.Sa.Pl@%<pl%>:@D.Px@@R.Sa.Pl@%>me          # ; ! Ellos son las casas

LEXICON PossPlural

@R.Px@@D.Sa.Pl@%<pl%>:@R.Px@@D.Sa.Pl@%>huan        # ; ! Sus casas
@R.Px@@R.Sa.Pl@%<pl%>:@R.Px@@R.Sa.Pl@%>huan        # ; ! Ellos son sus casas

LEXICON Plural 
                                                   PossPlural ;
                                                   PlainPlural ;
LEXICON Diminutivo

%<dim%>:%>tzin                                     # ; 
%<dim%>%<abs%>:%>tzin%>tli                         # ;  ! FIXME: Comprobar análisis ! Comprobar si se puede tener posesivo
%<dim%>%<pl%>:%>tzi%>tzin                          # ;  ! FIXME: Comprobar análisis

LEXICON Deriv 
                                                   Diminutivo ; 
LEXICON TlSuff
@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>tl    # ;
@D.Sa.Pl@@R.Px@%<n%>:@D.Sa.Pl@@R.Px@%>%{u%}%{h%}   # ;
%<n%>:                                             Plural ;
%<n%>:                                             Deriv ;

LEXICON ItlSuff

@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>itl   # ;
@D.Sa.Pl@@R.Px@%<n%>:@D.Sa.Pl@@R.Px@%>i%{u%}%{h%}  # ;
%<n%>:                                             Plural ;
%<n%>:                                             Deriv ;

LEXICON TliSuff

@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>tli   # ;
@D.Sa.Pl@@R.Px@%<n%>:@D.Sa.Pl@@R.Px@%>%{u%}%{h%}   # ;
%<n%>:                                             Plural ;
%<n%>:                                             Deriv ;

LEXICON LiSuff

@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>li    # ;
@D.Sa.Pl@@R.Px@%<n%>:@D.Sa.Pl@@R.Px@%>%{u%}%{h%}   # ;
%<n%>:%>li                                         Plural ;
%<n%>:                                             Deriv ;

LEXICON InSuff

@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>in    # ;
@D.Sa.Pl@@R.Px@%<n%>:@D.Sa.Pl@@R.Px@%>%{u%}%{h%}   # ;
%<n%>:                                             Plural ;
%<n%>:                                             Deriv ;

LEXICON NoSuff

@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>      # ;
@D.Sa.Pl@@R.Px@%<n%>:@D.Sa.Pl@@R.Px@%>%{u%}%{h%}   # ;
%<n%>:                                             Plural ;
%<n%>:                                             Deriv ;

!LEXICON Rel 
!
!+co%<n%>%<loc%>:%>co # ; ! FIXME: Orth
!+pan%<n%>%<loc%>:%>pan # ; ! FIXME: Orth
!+tlan%<n%>%<loc%>:%>tlan # ; ! FIXME: Orth
!+yan%<n%>%<loc%>:%>yan # ; ! FIXME: Orth

LEXICON DET

%<det%>: # ; 

LEXICON Personales
!
! Formas cortas de los pronombres personales singulares.
!
nehuatl%<prn%>%<pro%>%<p1%>%<sg%>:neh # ;
tehuatl%<prn%>%<pro%>%<p2%>%<sg%>:teh # ;
yehuatl%<prn%>%<pro%>%<p3%>%<sg%>:yeh # ;

!
! Formas largas de los pronombres personales singulares.
!
nehuatl%<prn%>%<pers%>%<p1%>%<sg%>:nehuatl # ;
tehuatl%<prn%>%<pers%>%<p2%>%<sg%>:tehuatl # ;
yehuatl%<prn%>%<pers%>%<p3%>%<sg%>:yehuatl # ;

!
! Pronombres personales plurales.
!
tehuan%<prn%>%<pers%>%<p1%>%<pl%>:tehuan # ;
nimehuan%<prn%>%<pers%>%<p2%>%<pl%>:nimehuan # ;
yehuan%<prn%>%<pers%>%<p3%>%<pl%>:yehuan # ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Determinantes 

in:in                                           DET ; ! "a" 

LEXICON Pronombres 

Personales ; 

!LEXICON Relacionales 
!
!kxi:kxi Rel ; ! FIXME: Orth
!nacas:nacas Rel ; ! FIXME: Orth
!jti:jti Rel ; ! FIXME: Orth

LEXICON Nouns

amatl:ama                                       TlSuff ; ! "papel, libro"
cihuatl:cihua                                   TlSuff ; ! "mujer"
oquichtl:oquich                                 TlSuff ; ! "varón"
tlacatl:tlaca                                   TlSuff ; ! "hombre"
tototl:toto                                     TlSuff ; ! "pájaro"
ihuitl:ihui                                     TlSuff ; ! "gallina"
tipetl:tipe                                     TlSuff ; ! "cerro"
cacalotl:cacalo                                 TlSuff ; ! "cuervo"
tapalcatihuilotl:tapalcatihuilo                 TlSuff ; ! "contenedor de agua"
tiquitl:tiqui                                   TlSuff ; ! "trabajo"
tlaltitl:tlalti                                 TlSuff ; ! "piedra de tierra"
atl:a                                           TlSuff ; ! "agua"
altepetl:altepe                                 TlSuff ; ! "ciudad, altepetl"
nacatl:naca                                     TlSuff ; ! "carne"
ichcatl:ichca                                   TlSuff ; ! "borrego"
tipoztl:tipoz                                   TlSuff ; ! "carro/algo metálico"
yolotl:yolo                                     TlSuff ; ! "corazon"
conetl:cone                                     TlSuff ; ! "bebe"
cozamalotl:cozamalo                             TlSuff ; ! "arcoiris"
ichpocaconetl:ichpocacone                       TlSuff ; ! "bebé niña"
quichconetl:quichcone                           TlSuff ; ! "bebé niño"
coyotl:coyo                                     TlSuff ; ! "coyote"
itl:i                                           TlSuff ; ! "frijol"
tihmahtl:tihlmah                                TlSuff ; ! "ropa/prenda"
ticactl:ticac                                   TlSuff ; ! "zapato"


tochtli:toch                                    TliSuff ; ! "conejo"
itzcuintli:itzcuin                              TliSuff ; ! "perro"
pahtli:pah                                      TliSuff ; ! "medicina"
poctli:poc                                      TliSuff ; ! "humo"
metztli:metz                                    TliSuff ; ! "luna"
mixtli:mix                                      TliSuff ; ! "nube

calli:cal                                       LiSuff ; ! "casa"
citlalli:citlal                                 LiSuff ; ! "estrella"

chapolin:chapol                                 InSuff ; ! "chapulín"
tamazolin:tamazol                               InSuff ; ! "rana"
ayotochin:ayotoch                               InSuff ; ! "armadillo"

tempil:tempil                                   NoSuff ; ! "pico"
mahtlapal:mahtlapal                             NoSuff ; ! "ala"
tonal:tonal                                     NoSuff ; ! "día, sol"
caxtil:caxtil                                   NoSuff ; ! "gallina"
patox:patox                                     NoSuff ; ! "pato"
ixtololo:ixtololo                               NoSuff ; ! "ojo"
tlapitlanil:tlapitlanil                         NoSuff ; ! "rayo"
icxi:icxi                                       NoSuff ; ! "pie"

xihuitl:xi%{u%}%{h%}                            ItlSuff ; ! "hierba, año"
caxitl:cax                                      ItlSuff ; ! "plato"
chiquihuitl:chiqui%{u%}%{h%}                    ItlSuff ; ! "canasta"


LEXICON Adj
iztac:izta               AdjCont ; !"blancx"  #TODO: i-deletion no ocurre aquí p.ej. "no+iztac" => noiztac, *noztac
xopiyahuac:xopiyahua     AdjCont ; !"verde"
tliltic:tlilti           AdjCont ; !"negrx"
chichiltic:chichilti     AdjCont ; !"rojx"
nixtic:nixti             AdjCont ; !"gris"


tomahuac:tomahua         AdjCont ; !"gordx"
pitzahuac:pitzahua       AdjCont ; !"delgadx"
chicactic:chicacti       AdjCont ; !"durx"
papatztic:papatzti       AdjCont ; !"suave"
xaxacuachtic:xaxacuachti AdjCont ; !"suave"
tzatzayactic:tzatzayacti AdjCont ; !"rotx"



LEXICON AdjCont
%<adj%>:c                               Diminutivo ;
@R.Sa.Sg@%<adj%>:@R.Sa.Sg@c             #;
@D.Sa@%<adj%>:@D.Sa@c                   #;
@D.Sa.Sg@%<adj%>%<pl%>:@D.Sa.Sg@%>que   # ;


LEXICON Puntuación

.%<sent%>:.   # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%¡%<sent%>:%¡ # ;
,%<cm%>:,     # ;
%?%<sent%>:%? # ;
…%<sent%>:…   # ;

