!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Multichar_Symbols

%<n%>                                    ! Sustantivo
%<v%>                                    ! Verbo
%<adj%>                                  ! Adjetivo
%<det%>                                  ! Determinante
%<prn%>                                  ! Pronombre

%<tv%>                                   ! Transitivo
%<iv%>                                   ! Intransitivo

%<pl%>                                   ! Plural
%<sg%>                                   ! Singular

%<abs%>                                  ! Absolutivo

%<loc%>                                  ! Locativo

%<pro%>                                  ! Pronombre proclítico
%<pers%>                                 ! Personal, forma larga

%<dim%>                                  ! Diminutivo

%<hon%>                                  ! Honorifico

%<p1%>
%<p2%>
%<p3%>

%<px1sg%>                                ! Posesivo (1ra singular)
%<px2sg%>
%<px3sg%>
%<px1pl%>
%<px2pl%>
%<px3pl%>

%<s_sg1%>                                ! Marcador de sujeto (1ra singular)
%<s_sg2%>                                ! Marcador de sujeto (2da singular)
%<s_sg3%>
%<s_pl1%>
%<s_pl2%>
%<s_pl3%>

%<o_sg1%>                               ! Marcador de objeto directo (1ra singular)
%<o_sg2%>                               ! Marcador de objeto directo (2da singular)
%<o_sg3%>
%<o_pl1%>
%<o_pl2%>
%<o_pl3%>

%<i_sg1%>                               ! Marcador de objeto indirecto (1ra singular)
%<i_sg2%>                               ! Marcador de objeto indirecto (2da singular)
%<i_sg3%>
%<i_pl1%>
%<i_pl2%>
%<i_pl3%>

%<o_aa3%>
%<o_nn3%>

%<pret%>
%<all%>                           ! direccional hacia el locutor

%<sent%>                                 ! Final de frase

%{o%}                                    ! o 
%{i%}                                    ! i / 0
%{h%}                                    ! h / u
%{u%}                                    ! u / h
%{q%}
%{U%}

%>                                       ! Frontera de morfema

@P.Px.Sg@                                ! Posesivo singular
@R.Px.Sg@
@P.Px.Pl@                                ! Posesivo plural
@R.Px.Pl@
@D.Px@                                   ! Ninguna posesión
@R.Px@
@P.Sa.Sg@                                ! Sujeto singular
@R.Sa.Sg@
@P.Sa.Pl@                                ! Sujeto plural
@R.Sa.Pl@
@P.Sa.2@                                 ! Sujeto es 2da persona (necesario para el honorífico)
@R.Sa.2@
@D.Sa@                                   ! Ningún sujeto
@D.Sa.Pl@ 
@D.Sa.Sg@
@U.Obj.Incorp@                            ! objecto es sustantivo incorporado

@P.DOBJ.FILLED@
@D.DOBJ@

@P.PST.ON@                          ! tiempo pasado
@R.PST@
@D.PST@
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

NominalSubject ;
Pronombres ;
Determinantes ;
Puntuación ;
Verb ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Verb
@P.PST.ON@:@P.PST.ON@o%>                             VerbalSubject ;
                                                     VerbalSubject ;

LEXICON VerbalSubject
@P.Sa.Sg@%<s_sg1%>:@P.Sa.Sg@ni%>                      Object ;
@P.Sa.Sg@@P.Sa.2@%<s_sg2%>:@P.Sa.Sg@@P.Sa.2@ti%>      Object ;
@P.Sa.Sg@%<s_sg3%>:@P.Sa.Sg@                          Object ;
@P.Sa.Pl@%<s_pl1%>:@P.Sa.Pl@ti%>                      Object ;
@P.Sa.Pl@%<s_pl2%>:@P.Sa.Pl@ni%>                      Object ;
@P.Sa.Pl@%<s_pl3%>:@P.Sa.Pl@                          Object ;
                                                      Object ;


LEXICON NominalSubject

! sg1: n-
! pl1: t-S-me / t-poss-S-huan

@P.Sa.Sg@%<s_sg1%>:@P.Sa.Sg@ni%>                      Predicate ;
@P.Sa.Sg@@P.Sa.2@%<s_sg2%>:@P.Sa.Sg@@P.Sa.2@ti%>      Predicate ;
@P.Sa.Sg@%<s_sg3%>:@P.Sa.Sg@                          Predicate ;
@P.Sa.Pl@%<s_pl1%>:@P.Sa.Pl@ti%>                      Predicate ;
@P.Sa.Pl@%<s_pl2%>:@P.Sa.Pl@ni%>                      Predicate ;
@P.Sa.Pl@%<s_pl3%>:@P.Sa.Pl@                          Predicate ;
                                                      Predicate ;

LEXICON Predicate
@R.Sa.2@:@R.Sa.2@                                     Honorific ;
                                                      Possession ;
                                                      Nominals ;

LEXICON Object

0:0                                                        IntransitiveDirectional ;

@P.DOBJ.FILLED@%<o_sg1%>:@P.DOBJ.FILLED@nech%>             TransitiveDirectional ;
@P.DOBJ.FILLED@%<o_sg2%>:@P.DOBJ.FILLED@mitz%>             TransitiveDirectional ;
@P.DOBJ.FILLED@%<o_sg3%>:@P.DOBJ.FILLED@%{q%}%{u%}%{i%}%>  TransitiveDirectional ;
@P.DOBJ.FILLED@%<o_pl1%>:@P.DOBJ.FILLED@tech%>             TransitiveDirectional ;
@P.DOBJ.FILLED@%<o_pl2%>:@P.DOBJ.FILLED@mech%>             TransitiveDirectional ;
@P.DOBJ.FILLED@%<o_pl3%>:@P.DOBJ.FILLED@qui%{m%}%>         TransitiveDirectional ;

%<i_sg1%>:nech%>                                    DirectObject ;
%<i_sg2%>:mitz%>                                    DirectObject ;
%<i_sg3%>:%{q%}%{u%}%{i%}%>                         DirectObject ;
%<i_pl1%>:tech%>                                    DirectObject ;
%<i_pl2%>:mech%>                                    DirectObject ;
%<i_pl3%>:qui%{m%}%>                                DirectObject ;


LEXICON DirectObject
@P.DOBJ.FILLED@%<o_pl3%>:@P.DOBJ.FILLED@i%{m%}%>   BitransitiveDirectional ;
                                                   BitransitiveDirectional ;

LEXICON IntransitiveDirectional
%<hon%>:on%>                                       IntransitiveVerbStem ;
%<all%>:hual%>                                     IntransitiveVerbStem ;
                                                   IntransitiveVerbStem ;
LEXICON TransitiveDirectional
%<hon%>:on%>                                       TransitiveIndefiniteObject ;  ! "away from" directional is used exclusively as an honorific marker
%<all%>:hual%>                                     TransitiveIndefiniteObject ;
                                                   TransitiveIndefiniteObject ;
LEXICON BitransitiveDirectional
%<hon%>:on%>                                       BitransitiveIndefiniteObject ;
%<all%>:hual%>                                     BitransitiveIndefiniteObject ;
                                                   BitransitiveIndefiniteObject ;
                                                   BitransitiveVerbStem ;

LEXICON TransitiveIndefiniteObject
@D.DOBJ@%<o_aa3%>:@D.DOBJ@te%>                     TransitiveVerbStem ;
@D.DOBJ@%<o_nn3%>:@D.DOBJ@tla%>                    TransitiveVerbStem ;
                                                   TransitiveVerbStem ;
LEXICON BitransitiveIndefiniteObject
@D.DOBJ@%<o_aa3%>:@D.DOBJ@te%>                     BitransitiveVerbStem ;
@D.DOBJ@%<o_nn3%>:@D.DOBJ@tla%>                    BitransitiveVerbStem ;
                                                   BitransitiveVerbStem ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Continuation Verb Endings
!--------------------------
! * PresentTense
! * Imperfect
!   (In classical nahuatl, this is the same as PresentTense base (Base 1). In
!    nhi, verbs ending in 'ia' or 'oa' drop the first of those two ('i' and
!    'o', respectively), as is done in Base 1 in classical Nahuatl.
!    for eg:
!       ilhuia, "contar/decir"
!       classical:      o-ni-mitz-ilhuia-ya
!       western sierra: o-ni-mitz-ilhua-ya
!       "Yo te (lo) decía"

! * Base2Suffixes
!       * Preterite
!       * Pluperfect

! * Base3Suffixes
!       * Future
!       * Counterfactual
!       * Optative
!       * SuffixalDirectional


LEXICON IntransitiveVerbStem
cochi:cochi%>  PresentTense ;
cochi:cochi%>  Imperfect ;
cochi:coch%>   Base2Suffixes ;
cochi:cochi%>  Base3Suffixes ;

quiza:quiza%>  PresentTense ;
quiza:quiza%>  Imperfect ;
!quiza:quiz%>  Base2Suffixes ;
!quiza:quiza%> Base3Suffixes ;


LEXICON TransitiveVerbStem
itta:itta%>     PresentTense ;
itta:itta%>     Imperfect ;
itta:itta%>     Base2Suffixes ;
itta:itta%>     Base3Suffixes ;

tlalia:tlalia%> PresentTense ;
tlalia:tlala%>  Imperfect ;
tlalia:tlali%>  Base2Suffixes ;
tlalia:tlali%>  Base3Suffixes ;


LEXICON BitransitiveVerbStem
maca:maca%> PresentTense ;
maca:maca%> Imperfect ;
maca:maca%> Base2Suffixes ;
maca:maca%> Base3Suffixes ;

ilhuia:ilhuia%> PresentTense ;
ilhuia:ilhua%>  Imperfect ;
ilhuia:ilhuih%> Base2Suffixes ;
ilhuia:ilhui%>  Base3Suffixes ;


!
!
!
!!!!!!!!!!!!!!!!!!!!!
LEXICON Base2Suffixes
              Preterite ;
              Pluperfect ;

LEXICON Base3Suffixes
              Future ;
              Counterfactual ;
              Optative ;
              ! TODO Suffixal Directionals (Perfective/Imperfective/Optative)


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON PresentTense
@D.PST@%<pres%>:@D.PST@0  # ;
@R.Sa.Pl@@D.PST@%<pres%>:@R.Sa.Pl@@D.PST@h # ;

LEXICON Imperfect
@D.Sa.Pl@@R.PST@%<ipfv%>:@D.Sa.Pl@@R.PST@ya  # ;
@R.Sa.Pl@%<ipfv%>:@R.Sa.Pl@yah # ;

LEXICON Preterite
@D.Sa.Pl@%<Pret%>:@D.Sa.Pl@%{c%}  # ;
@R.Sa.Pl@%<Pret%>:@R.Sa.Pl@que # ;

LEXICON Pluperfect
@D.Sa.Pl@%<Plup%>:@D.Sa.Pl@ca   # ;
@R.Sa.Pl@%<Plup%>:@R.Sa.Pl@cah  # ;

LEXICON Future
@D.PST@@D.Sa.Pl@%<Fut%>:@D.PST@@D.Sa.Pl@z    # ;
@D.PST@@R.Sa.Pl@%<Fut%>:@D.PST@@R.Sa.Pl@zque # ;

LEXICON Counterfactual
@D.PST@@D.Sa.Pl@%<Cfact%>:@D.PST@@D.Sa.Pl@zquia # ;
@D.PST@@R.Sa.Pl@%<Cfact%>:@D.PST@@R.Sa.Pl@zquiah # ;

LEXICON Optative
@D.PST@@D.Sa.Pl@:@D.PST@@D.Sa.Pl@0   # ;
@D.PST@@R.Sa.Pl@:@D.PST@@R.Sa.Pl@can # ;




LEXICON Possession

@P.Px.Sg@%<px1sg%>:@P.Px.Sg@n%{o%}%>               Nominals ;
@P.Px.Sg@%<px2sg%>:@P.Px.Sg@m%{o%}%>               Nominals ;
@P.Px.Sg@%<px3sg%>:@P.Px.Sg@i%>                    Nominals ;
@P.Px.Pl@%<px1pl%>:@P.Px.Pl@t%{o%}%>               Nominals ;
@P.Px.Pl@%<px2pl%>:@P.Px.Pl@nim%{o%}%>             Nominals ;
@P.Px.Pl@%<px3pl%>:@P.Px.Pl@im%>                   Nominals ;

LEXICON Nominals 
! Sustantivos, sustantivos relacionales y otras categorias que
! pueden llevar posesión
                                                   Nouns ;
                                                   Adj ;

LEXICON Honorific !TODO: add i -> honorific vowel harmony in .twolc file.
%<hon%>:on%>                   Possession ;
%<hon%>:on%>                   Nominals ;


LEXICON PlainPlural

@D.Px@@D.Sa@%<pl%>:@D.Px@@D.Sa@%>me                # ; ! Las casas
@D.Px@@R.Sa.Pl@%<pl%>:@D.Px@@R.Sa.Pl@%>me          # ; ! Ellos son las casas

LEXICON PossPlural

@R.Px@@D.Sa.Pl@%<pl%>:@R.Px@@D.Sa.Pl@%>huan        # ; ! Sus casas
@R.Px@@R.Sa.Pl@%<pl%>:@R.Px@@R.Sa.Pl@%>huan        # ; ! Ellos son sus casas

LEXICON Plural 
                                                   PossPlural ;
                                                   PlainPlural ;
LEXICON Diminutivo

%<dim%>:%>tzin                                     # ; 
%<dim%>%<abs%>:%>tzin%>tli                         # ;  ! FIXME: Comprobar análisis ! Comprobar si se puede tener posesivo
%<dim%>%<pl%>:%>tzi%>tzin                          # ;  ! FIXME: Comprobar análisis

LEXICON Deriv 
                                                   Diminutivo ; 
LEXICON TlSuff
@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>tl    # ;
@D.Sa.Pl@%<n%>:@D.Sa.Pl@%>%{u%}%{h%}               # ;
%<n%>:                                             Plural ;
%<n%>:                                             Deriv ;

LEXICON ItlSuff

@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>itl   # ;
@D.Sa.Pl@%<n%>:@D.Sa.Pl@%>%{u%}%{h%}               # ;
%<n%>:                                             Plural ;
%<n%>:                                             Deriv ;

LEXICON TliSuff

@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>tli   # ;
@D.Sa.Pl@%<n%>:@D.Sa.Pl@%>%{u%}%{h%}               # ;
%<n%>:                                             Plural ;
%<n%>:                                             Deriv ;

LEXICON LiSuff

@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>li    # ;
@D.Sa.Pl@%<n%>:@D.Sa.Pl@%>%{u%}%{h%}               # ;
%<n%>:%>li                                         Plural ;
%<n%>:                                             Deriv ;

LEXICON InSuff

@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>in    # ;
@D.Sa.Pl@%<n%>:@D.Sa.Pl@%>%{u%}%{h%}               # ;
%<n%>:                                             Plural ;
%<n%>:                                             Deriv ;

LEXICON NoSuff

@D.Px@@D.Sa.Pl@%<n%>%<abs%>:@D.Px@@D.Sa.Pl@%>      # ;
@D.Sa.Pl@%<n%>:@D.Sa.Pl@%>%{u%}%{h%}               # ;
%<n%>:                                             Plural ;
%<n%>:                                             Deriv ;

!LEXICON Rel 
!
!+co%<n%>%<loc%>:%>co # ; ! FIXME: Orth
!+pan%<n%>%<loc%>:%>pan # ; ! FIXME: Orth
!+tlan%<n%>%<loc%>:%>tlan # ; ! FIXME: Orth
!+yan%<n%>%<loc%>:%>yan # ; ! FIXME: Orth

LEXICON DET

%<det%>: # ; 

LEXICON Personales
!
! Formas cortas de los pronombres personales singulares.
!
nehuatl%<prn%>%<pro%>%<p1%>%<sg%>:neh # ;
tehuatl%<prn%>%<pro%>%<p2%>%<sg%>:teh # ;
yehuatl%<prn%>%<pro%>%<p3%>%<sg%>:yeh # ;

!
! Formas largas de los pronombres personales singulares.
!
nehuatl%<prn%>%<pers%>%<p1%>%<sg%>:nehuatl # ;
tehuatl%<prn%>%<pers%>%<p2%>%<sg%>:tehuatl # ;
yehuatl%<prn%>%<pers%>%<p3%>%<sg%>:yehuatl # ;

!
! Pronombres personales plurales.
!
tehuan%<prn%>%<pers%>%<p1%>%<pl%>:tehuan # ;
nimehuan%<prn%>%<pers%>%<p2%>%<pl%>:nimehuan # ;
yehuan%<prn%>%<pers%>%<p3%>%<pl%>:yehuan # ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Determinantes 

in:in                                           DET ; ! "a" 

LEXICON Pronombres 

Personales ; 

!LEXICON Relacionales 
!
!kxi:kxi Rel ; ! FIXME: Orth
!nacas:nacas Rel ; ! FIXME: Orth
!jti:jti Rel ; ! FIXME: Orth

LEXICON Nouns

amatl:ama                                       TlSuff ; ! "papel, libro"
cihuatl:cihua                                   TlSuff ; ! "mujer"
oquichtl:oquich                                 TlSuff ; ! "varón"
tlacatl:tlaca                                   TlSuff ; ! "hombre"
tototl:toto                                     TlSuff ; ! "pájaro"
ihuitl:ihui                                     TlSuff ; ! "gallina"
tipetl:tipe                                     TlSuff ; ! "cerro"
cacalotl:cacalo                                 TlSuff ; ! "cuervo"
tapalcatihuilotl:tapalcatihuilo                 TlSuff ; ! "contenedor de agua"
tiquitl:tiqui                                   TlSuff ; ! "trabajo"
tlaltitl:tlalti                                 TlSuff ; ! "piedra de tierra"
atl:a                                           TlSuff ; ! "agua"
altepetl:altepe                                 TlSuff ; ! "ciudad, altepetl"
nacatl:naca                                     TlSuff ; ! "carne"
ichcatl:ichca                                   TlSuff ; ! "borrego"
tipoztl:tipoz                                   TlSuff ; ! "carro/algo metálico"
yolotl:yolo                                     TlSuff ; ! "corazon"
conetl:cone                                     TlSuff ; ! "bebe"
cozamalotl:cozamalo                             TlSuff ; ! "arcoiris"
ichpocaconetl:ichpocacone                       TlSuff ; ! "bebé niña"
quichconetl:quichcone                           TlSuff ; ! "bebé niño"
coyotl:coyo                                     TlSuff ; ! "coyote"
itl:i                                           TlSuff ; ! "frijol"
tihmahtl:tihlmah                                TlSuff ; ! "ropa/prenda"
ticactl:ticac                                   TlSuff ; ! "zapato"


tochtli:toch                                    TliSuff ; ! "conejo"
itzcuintli:itzcuin                              TliSuff ; ! "perro"
pahtli:pah                                      TliSuff ; ! "medicina"
poctli:poc                                      TliSuff ; ! "humo"
metztli:metz                                    TliSuff ; ! "luna"
mixtli:mix                                      TliSuff ; ! "nube

calli:cal                                       LiSuff ; ! "casa"
citlalli:citlal                                 LiSuff ; ! "estrella"

chapolin:chapol                                 InSuff ; ! "chapulín"
tamazolin:tamazol                               InSuff ; ! "rana"
ayotochin:ayotoch                               InSuff ; ! "armadillo"

tempil:tempil                                   NoSuff ; ! "pico"
mahtlapal:mahtlapal                             NoSuff ; ! "ala"
tonal:tonal                                     NoSuff ; ! "día, sol"
caxtil:caxtil                                   NoSuff ; ! "gallina"
patox:patox                                     NoSuff ; ! "pato"
ixtololo:ixtololo                               NoSuff ; ! "ojo"
tlapitlanil:tlapitlanil                         NoSuff ; ! "rayo"
icxi:icxi                                       NoSuff ; ! "pie"

xihuitl:xi%{u%}%{h%}                            ItlSuff ; ! "hierba, año"
caxitl:cax                                      ItlSuff ; ! "plato"
chiquihuitl:chiqui%{u%}%{h%}                    ItlSuff ; ! "canasta"


LEXICON Adj
iztac:izta               AdjCont ; !"blancx"  #TODO: i-deletion no ocurre aquí p.ej. "no+iztac" => noiztac, *noztac
xopiyahuac:xopiyahua     AdjCont ; !"verde"
tliltic:tlilti           AdjCont ; !"negrx"
chichiltic:chichilti     AdjCont ; !"rojx"
nixtic:nixti             AdjCont ; !"gris"


tomahuac:tomahua         AdjCont ; !"gordx"
pitzahuac:pitzahua       AdjCont ; !"delgadx"
chicactic:chicacti       AdjCont ; !"durx"
papatztic:papatzti       AdjCont ; !"suave"
xaxacuachtic:xaxacuachti AdjCont ; !"suave"
tzatzayactic:tzatzayacti AdjCont ; !"rotx"



LEXICON AdjCont
%<adj%>:c                               Diminutivo ;
@R.Sa.Sg@%<adj%>:@R.Sa.Sg@c             #;
@D.Sa@%<adj%>:@D.Sa@c                   #;
@D.Sa.Sg@%<adj%>%<pl%>:@D.Sa.Sg@%>que   # ;


LEXICON Puntuación

.%<sent%>:.   # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%¡%<sent%>:%¡ # ;
,%<cm%>:,     # ;
%?%<sent%>:%? # ;
…%<sent%>:…   # ;

