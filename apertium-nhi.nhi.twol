Alphabet
 
 A B C D E F G H I J K L M N O P Q R S T U W X Y Z
 a b c d e f g h i j k l m n o p q r s t u w x y z
 %{i%}:i %{i%}:0
 %{h%}:h %{h%}:u %{H%}:0 %{H%}:h
 %{u%}:u %{u%}:h
 %{U%}:u %{U%}:0
 %{q%}:q %{q%}:c
 %{m%}:m %{m%}:n
 %{c%}:c %{c%}:0
 %{e%}:e %{e%}:0
 %{z%}:z %{z%}:c
 %>:%> %{P%}:0 ;


Sets

Vow   = A E I O U 
        a e i o u ;

Cns   = b c d f g h j k l m n p q r s t w x y z ;


Rules

"Simple vowel-harmony triggered by honorific"
i:o <=> [ n | t | x ] _ >:> %{q%}:c %{u%}:0 %{i%}:0 >:> o n >:> ;
!@ n i:o >:> {q}:c {u}:0 {i}:0 >:> o n >:> i t a
!@ x i:o >:> {q}:c {u}:0 {i}:0 >:> o n >:> c a q u i

"z->c when in plural for spanish nouns e.g. vez -> veces"
%{z%}:c <=> _ >:> %{e%}: s ;

"Plural Spanish Nouns"
%{e%}:0 <=> Vow >:> _ s ;
!@ n o >:> c o m p a Ã± e r o >:> {e}:0 s

"Nasal assimilation"
n:0 <=> _ %>: m e ;

"Drop i in subj pref before on-"
i:0 <=> t _ %>: o n %>: ;



"stem-initial i deletes preceding o from reflexive mo (1)"
%{i%}:i <=> m o %>: _ ;

"stem-initial i deletes preceding o from reflexive mo (2)"
o:0 <=> m _ %>: %{i%}:i  ;
!@ m o:0 >:> i:i t o n i a


"Nasal elision"
n:0 <=> _ %>: h u a n ;
!@ n o >:> i:0 t z c u i n:0 >:> h u a n


"/i/ deletion"
i:0 <=> [n | m | t] o >:> _ [ [ Cns Cns ] - [ t z | t l | c h ] | t l Cns | t z Cns | c h Cns] ;
!@ n o >:> i t z i n
!@ n o >:> i:0 t z c u i n

"delete preterite 'c' ending when preceded by a cons"
%{c%}:0 <=> [Cns: | %{H%}: | %{P%}:0] >:> _ .#. ;

"ih -> e in preterite (1)"
i:e <=> _ %{H%}: %{P%}:0 >:> %{c%}:0 ;


"q:q at beginning of word in qui"
%{q%}:q <=> .#. _ %{U%}: %{i%}: >:> Cns ;

"u:u at beginning of word in qui"
%{U%}:u <=> .#. %{q%}:q _ %{i%}: >:> Cns ;

"i:i at beginning of word in qui"
%{i%}:i <=> .#. %{q%}:q %{U%}:u _ >:> Cns ;


"'q' -> 'c' in /k/ between a V and a C "
%{q%}:c <=> Vow >:> _ %{U%}: %{i%}: >:> Cns ;

"'q' -> 'c' in /k/ when followed by a V "
%{q%}:c <=> >:> _ %{U%}: %{i%}: >:> [a | o | u] ;

"u -> 0 in /k/ between V and C"
%{U%}:0 <=> Vow >:> %{q%}: _ %{i%}: >:> Cns ;

"u -> 0 in /k/ when followed by a V"
%{U%}:0 <=> >:> %{q%}: _ %{i%}: >:> [a | o | u] ;

"i -> 0 in /k/ between V and C"
%{i%}:0 <=> Vow >:> %{q%}: %{U%}: _ >:> Cns ;

"i -> 0 in /k/ when followed by a front vowel"
%{i%}:0 <=> %{q%}: %{U%}: _ >:> [i | e] ;

"Weak /i/ drops after tla"
i:0 <=> t l a >:> _ h ;

"Possessive nasal assimilation"
m:n <=> i _ %>: [ c | t | n | s | y ] ;
!@ i m:n >: c a l

"nasal place assimilation in object prefix"
 %{m%}:m <=> _ %>: [Vow | p: | b: | m:];

"Possessive ending [w] after vowel"
%{u%}:0 <=> Cns %>: _ %{h%}: .#. ;

"Possessive ending [w] after vowel second component"
%{h%}:0 <=> Cns %>: %{u%}:0 _ .#. ;
 
!@ n o >:> t i p o z >:> {u}:0 {h}:0

! "i->e present plural"
! i:e <=> Cns _ >:> %{H%}: .#. ;

! "drop h after i->e in present plural"
! %{H%}:0 <=> Cns i >:> _ .#. ;


!"h->0 pret sg"
!%{H%}:0 <=> %{i%}:e _ >:> %{c%}: .#. ;

"Metathesis of -{h}{u} for {h}"
%{u%}:h <= %{h%}:u _ [Cns | .#.];

!@ n o >:> x i {u}:u {h}:h

"Metathesis of -{h}{u} for {u}"
%{h%}:u <=> _ %{u%}:h [Cns | .#.]; 


"inv. Metathesis of -{h}{u} for {h}"
%{u%}:u <= %{h%}:h _ [Vow | >:];

!@ n o >:> x i {u}:u {h}:h

"inv. Metathesis of -{h}{u} for {u}"
%{h%}:h <=> _ %{u%}:u [Vow | >:]; 
