! Grapheme to phoneme convertor for the ACK orthography.
! Weights are used to favour longer matches.
Multichar_Symbols

%>

LEXICON Root

Tetragraphs ;

LEXICON Loop 

        Tetragraphs "weight: 0.0" ;
n:n̥     # "weight: 0.0" ; 
        # ;

LEXICON Tetragraphs 

cqui:hki Loop "weight: 0.0" ;
         Trigraphs "weight: 0.3" ;

LEXICON Trigraphs 

que:ke   Loop "weight: 0.0" ;
qui:ki   Loop "weight: 0.0" ;
cua:kʷa  Loop "weight: 0.0" ;
hua:wa   Loop "weight: 0.0" ;
         Digraphs "weight: 0.6" ;

LEXICON Digraphs

za:sa Loop   "weight: 0.0" ;
zo:so Loop  "weight: 0.0" ;
ce:se Loop  "weight: 0.0" ;
ci:si Loop  "weight: 0.0" ;
ca:ka Loop  "weight: 0.0" ;
co:ko Loop  "weight: 0.0" ;
cu:ku Loop  "weight: 0.0" ;
tz:ts Loop  "weight: 0.0" ;
ch:tʃ Loop  "weight: 0.0" ;
tl:tɬ Loop  "weight: 0.0" ;
lt:ɬt Loop  "weight: 0.0" ;
tt:ht Loop  "weight: 0.0" ;
ll:l  Loop  "weight: 0.0" ;
uh:h  Loop  "weight: 0.0" ;
      Graphs  "weight: 1.0" ;

LEXICON Graphs 
m:m  Loop ;
n:n  Loop ;
p:p  Loop ;
t:t  Loop ;
x:ʃ  Loop ;
r:r  Loop ;
h:h  Loop ;
l:l  Loop ;
y:j  Loop ;
z:s  Loop ;
g:g  Loop ;
c:k  Loop ;

a:a  Loop ;
e:e  Loop ;
i:i  Loop ;
o:o  Loop ;
u:u  Loop ;
     Loop ;

